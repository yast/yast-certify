/**
 * File:	include/certify/ui.ycp
 * Package:	Certification
 * Summary:	User interface functions
 * Authors:	Ludwig Nussel <lnussel@suse.de>
 *
 * $Id$
 */

{

textdomain "certify";

import "Certify";
import "Label";
import "Popup";
import "Progress";
import "Sequencer";
import "Wizard";

include "certify/helps.ycp";
include "certify/ui_system.ycp";
include "certify/ui_tests.ycp";

define any Self_certifySequence ();
define any Self_certifyAutoSequence ();
define any MainSequence ();
define symbol ReadDialog ();
define symbol WriteDialog ();
define any SummaryDialog ();

/**
 * Whole configuration of self_certify
 * @return any Returned value from WizardSequencer() call
 */
define any Self_certifySequence () ``{
    map aliases =
	$[
	    "read"	: [ ``( ReadDialog () ), true ],
	    "main"	:   ``( MainSequence () ),
	    "write"	: [ ``( WriteDialog () ), true ]
	];

    map sequence = $[
	"ws_start" : "read",
	"read" :
	$[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" :
	$[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    string caption = _("Self Certify Configuration");
    term contents = `Label (_("Initializing ..."));

    Wizard::CreateDialog ();
    Wizard::SetDesktopIcon("certify");
    Wizard::SetContentsButtons ( caption,
				contents,
				"",
				Label::BackButton (),
				Label::NextButton ());

    any ret = Sequencer::Run (aliases, sequence);

    UI::CloseDialog ();
    return ret;
}

/**
 * Whole configuration of self_certify but without reading and writing.
 * For use with autoinstallation.
 * @return any Returned value from WizardSequencer() call
 */
define any Self_certifyAutoSequence () ``{
    string caption = _("Self Certify Configuration");
    term contents = `Label (_("Initializing ..."));

    Wizard::CreateDialog ();
    Wizard::SetDesktopIcon("certify");
    Wizard::SetContentsButtons ( caption,
				contents,
				"",
				Label::BackButton (),
				Label::NextButton ());

    // Run the main configuration workflow
    any ret = MainSequence ();

    UI::CloseDialog ();
    return ret;
}

/**
 * Main workflow of the self_certify configuration
 * @return any Returned value from WizardSequencer() call
 */
define any MainSequence () ``{
    map aliases =
	$[
	    "system"	:   ``( SystemDialog () ),
	    "mainboard"	:   ``( MainboardDialog () ),
	    "hdd"	:   ``( HDDDialog () ),
	    "chipset"	:   ``( ChipsetDialog () ),
	    "misc"	:   ``( MiscDialog () ),
	    "collect"	:   ``( CollectDialog () ),
	    "ping"	:   ``( PingDialog () ),
	    "floppy"	:   ``( FloppyDialog () ),
	    "cdrom"	:   ``( CdromDialog () ),
	    "serial"	:   ``( SerialDialog () ),
	    "kernel"	:   ``( KernelDialog () ),
	    "bonnie"	:   ``( BonnieDialog () ),
	    "summary"	:   ``( SummaryDialog () ),
//	    "edit"	: [ ``( AddSequence () ), true ]
	];

    map sequence = $[
	"ws_start" : "system",
	"system" :
	$[
	    `abort	: `abort,
	    `next	: "mainboard"
	],
	"mainboard" :
	$[
	    `abort	: `abort,
	    `next	: "hdd"
	],
	"hdd" :
	$[
	    `abort	: `abort,
	    `next	: "chipset"
	],
	"chipset" :
	$[
	    `abort	: `abort,
	    `next	: "misc"
	],
	"misc" :
	$[
	    `abort	: `abort,
	    `next	: "collect"
	],
	"collect" :
	$[
	    `abort	: `abort,
	    `next	: "ping"
	],
	"ping" :
	$[
	    `abort	: `abort,
	    `next	: "floppy"
	],
	"floppy" :
	$[
	    `abort	: `abort,
	    `next	: "cdrom"
	],
	"cdrom" :
	$[
	    `abort	: `abort,
	    `next	: "serial"
	],
	"serial" :
	$[
	    `abort	: `abort,
	    `next	: "kernel"
	],
	"kernel" :
	$[
	    `abort	: `abort,
	    `next	: "bonnie"
	],
	"bonnie" :
	$[
	    `abort	: `abort,
	    `next	: "summary"
	],
	"summary" :
	$[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    any ret = Sequencer::Run (aliases, sequence);

    return ret;
}

/**
 * Read settings dialog
 * @return symbol `next if success, else `abort
 */
define symbol ReadDialog () ``{
    // Set help text
//    Wizard::RestoreHelp (ReadDialogHelp ());

    // A callback function for abort
    any callback = ``{
	return UI::PollInput () == `abort;
    };

    // Read the configuration
    boolean was_ok = Certify::Read ( callback );

    if ( Certify::test_mode != true )
    {
	integer cmdret = (integer) SCR::Execute (.target.bash,"/usr/bin/connecttox");
	if (cmdret>0)
	{
	    Popup::Error( _("You must run this from X."));
	    return `abort;
	}
    }


    return ( was_ok? `next : `abort );
}

/**
 * Write settings dialog
 * @return symbol `next if success, else `abort
 */
define symbol WriteDialog () ``{
    // Set help text
    Wizard::RestoreHelp (WriteDialogHelp ());
    Wizard::DisableAbortButton();
    Wizard::DisableNextButton();
    Wizard::DisableBackButton();

    // A callback function for abort
    any callback = ``{
	return UI::PollInput () == `abort;
    };

    // Read the configuration
    boolean was_ok = Certify::Write ( callback );

    return ( was_ok? `next : `abort );
}

/**
 * SummaryDialog
 * @return any Returned value from UserInput() call
 */
define any SummaryDialog () ``{
    string caption = _("Certification: Summary");

    term contents = `VBox(
	`VSpacing(1),
	`Label(_("Insert a blank floppy in fd0 and press Next.")),
	`VSpacing(1),
	`RichText(Certify::Summary())
    );

    Wizard::SetContentsButtons ( caption,
				contents,
				SummaryDialogHelp(),
				Label::BackButton (),
				Label::NextButton () );

    any ret = nil;
    while (true)
    {
	ret = UI::UserInput ();
	if (ret == `abort)
	{
	    if ( Popup::ReallyAbort ( true ) )
		break;
	    else
		continue;
	}
	else
	{
	    break;
	}
    };

    return ret;
}

/* EOF */
}
